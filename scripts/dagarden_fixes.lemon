function void Character.updateJumpMoves()
{
	if (IsP1Sawnic() || isExtraSlot())
	{
		base.Character.updateJumpMoves()
	}
	else
	{
		u8 input_state     = (char.character == CHARACTER_TAILS) ? control.tails.state   : control.player1.state
		u8 input_pressed   = (char.character == CHARACTER_TAILS) ? control.tails.pressed : control.player1.pressed
		bool isSuperActive = (char.character == CHARACTER_TAILS) ? super.active.tails    : super.active

		if (char.jumping)
		{
			s16 max_vy = (char.flags & char.flag.UNDERWATER) ? -0x200 : -0x400
			if (char.velocity.y < max_vy)
			{
				if ((input_state & CONTROL_ABC) == 0)
				{
					char.velocity.y = max_vy
				}
			}
			else
			{
				if (char.double_jump_state == 0)
				{
					if (Input.buttonPressed(BUTTON_Y))
					{
						if (!isSuperActive && (!Mods.isModActive("Sonic 3: D.A. Garden Edition") || (Mods.isModActive("Sonic 3: D.A. Garden Edition") && !original_mode)))
						{
							if (Character.performSuperTransformation())
								return
						}
						else
							base.Character.updateJumpMoves()
					}
					if (input_pressed & CONTROL_ABC)
					{
						if (onCharacterPressedJumpInMidAir(input_state, input_pressed))
						{
							if (char.character == CHARACTER_SONIC)
							{
								SonicPressedJumpInMidAir()
							}
							else if (char.character == CHARACTER_TAILS)
							{
								TailsPressedJumpInMidAir()
							}
							else if (!competition_mode.active)
							{
								KnucklesPressedJumpInMidAir()
							}
							else
							{
								SonicPressedJumpInMidAir()
							}
						}
					}
				}
				if (Game.getSetting(SETTING_DROPDASH) && char.character == CHARACTER_SONIC && !competition_mode.active && !original_mode)
				{
					base.Character.updateJumpMoves()
				}
			}
		}
		else
		{
			if (char.spindash == 0)
			{
				char.velocity.y = max(char.velocity.y, -0x0fc0)
			}
		}
	}
}

function bool isExtraSlot()
{
	return base.isExtraSlot()
}

function bool IsP1Sawnic()
{
	return base.IsP1Sawnic()
}